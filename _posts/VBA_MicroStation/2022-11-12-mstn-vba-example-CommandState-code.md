---
title: MicroStation VBA示例-CommandState
author: QinDong
date: 2022-11-12 11:18:00 +0800
categories: 编程开发 VBA-MicroStation
tags: 示例代码
excerpt: 
---
* content
{:toc}

### StartLocate
To implement an element-modification command, create a class that implements the ILocateCommandEvents interface. To activate the command, create an instance of the command and call StartLocate passing in that instance as the argument. 


#### Copy Element Example
This example illustrates the implementation of an element modification command. The following code starts the command by creating an instance of the class that implements ILocateCommandEvents and invoking StartLocate passing in that instance as an argument. 

```vb
Sub RunCopyElement()
    CommandState.StartLocate New clsCopyElementCommand
End Sub
```
{: .nolineno}

This following code must be in a class that implements ILocateCommandEvents. In this example, it is in the class clsCopyElementCommand. 

```vb
Implements ILocateCommandEvents

Private m_oStartElement As Element
Private m_tStartPoint As Point3d

'  Both the Dynamics and the Accept event handlers use this
'  method to create a copy of the element and move as far
'  as the cursor has moved.
Function CreateMovedElement(oBase As Element, tInputPoint As Point3d, bAddToModel As Boolean) As Element
    Dim tDistance As Point3d
    Dim oContext As New CopyContext

    With tDistance
        .X = tInputPoint.X - m_tStartPoint.X
        .Y = tInputPoint.Y - m_tStartPoint.Y
        .Z = tInputPoint.Z - m_tStartPoint.Z
    End With

    oContext.AddElementToModel = bAddToModel
    '  CopyElement's default behavior is to add the element to the model. Use
    '  the CopyContext to control whether or not it adds the element to the model.
    Set CreateMovedElement = ActiveModelReference.CopyElement(m_oStartElement, oContext)
    CreateMovedElement.Move tDistance

End Function


' This is called when the user enters a data point after the
'  LocateFilter has accepted an element
Private Sub ILocateCommandEvents_Accept(ByVal Element As Element, _
                Point As Point3d, ByVal View As View)
    Dim oTemp As Element
    Set oTemp = CreateMovedElement(m_oStartElement, Point, True)

    oTemp.Rewrite
    oTemp.Redraw
    CommandState.StartLocate New clsCopyElementCommand
End Sub

Private Sub ILocateCommandEvents_Cleanup()

End Sub

Private Sub ILocateCommandEvents_Dynamics(Point As Point3d, _
        ByVal View As View, ByVal DrawMode As MsdDrawingMode)
    Dim oTemp As Element

    Set oTemp = CreateMovedElement(m_oStartElement, Point, False)
    oTemp.Redraw DrawMode
End Sub

Private Sub ILocateCommandEvents_LocateFailed()
    CommandState.StartLocate New clsCopyElementCommand
End Sub


Private Sub ILocateCommandEvents_LocateFilter(ByVal Element As Element, _
        Point As Point3d, Accepted As Boolean)
    ' If this method does not set Accepted to False, then
    ' the element is accepted. This functions remembers what
    ' element was accepted and the datapoint used to accept
    ' the element.  Then it starts dynamics.
    Set m_oStartElement = Element
    m_tStartPoint = Point
    CommandState.StartDynamics
End Sub

Private Sub ILocateCommandEvents_LocateReset()
    CommandState.StartLocate New clsCopyElementCommand
End Sub

Private Sub ILocateCommandEvents_Start()
    Dim lc As LocateCriteria

    '  Since this command does not modify the original element,
    '  set the locate criteria to allow read-only elements.
    Set lc = CommandState.CreateLocateCriteria(False)
    CommandState.SetLocateCriteria lc

    '  MicroStation disables AccuSnap whenever a new command starts, so a command
    '  should enable AccuSnap whenever it is appropriate.  This user still has the
    '  ability to turn off AccuSnap. Enabling AccuSnap does not override that.
    '  EnableAccuSnap only enables AccuSnap if the user has it turned on.
    CommandState.EnableAccuSnap

    ShowCommand "Copy Element"
    ShowPrompt "Select element to Copy"
End Sub
```
{: .nolineno}

### StartPrimitive
To implement an element-creation command, create a class that implements the IPrimitiveCommandEvents interface. To activate the command, create an instance of the command and call StartPrimitive passing in that instance as the argument. 

### Dropping an Element Example
This example illustrates how to replace an element with the elements generated by invoking Drop. The example is a simple primitive command that implements event handlers for the start and data point events. Because the other event handlers of IPrimitiveCommandEvents do nothing in this example, they have been omitted. 

Most of the logic for this example is in the data point event handler. It calls LocateElement to find an element that the data point selects. Then it invokes IsDroppableElement to determine if the element can be dropped. If it can be dropped, the data point handler invokes Drop to get an ElementEnumerator object that can be used to retrieve the elements extracted from the element being dropped. Then it invokes RemoveElement to remove the element from the model, and to erase it from the screen. Finally, it advances through the extracted elements calling AddElement to add each one to the model. 

The start event handler displays a prompt and command name. Next it turns on the locate cursor by calling SetLocateCursor. 

The start event handler has to do these things every time the command is restarted because MicroStation always resets the command state every time a new primitive or locate command is started. MicroStation sets the command state back to the default and then calls the start event handler to give it a chance to establish the command state. 

```vb
Implements IPrimitiveCommandEvents

'
'  Data point event handler
'
Private Sub IPrimitiveCommandEvents_DataPoint(point As Point3d, ByVal View As View)
    Dim oEle As Element
    ShowStatus ""
    On Error GoTo NoElement

    Set oEle = CommandState.LocateElement(point, View, True)
    If oEle.IsDroppableElement Then
        Dim oNew As Element
        Dim oDE As DroppableElement
        Dim oEE As ElementEnumerator

        Set oDE = oEle
        Set oEE = oDE.Drop
        ActiveModelReference.RemoveElement oEle
        Do While oEE.MoveNext
            Set oNew = oEE.Current
            ActiveModelReference.AddElement oNew
            oNew.Redraw msdDrawingModeNormal
        Loop
        CommandState.StartPrimitive Me
    Else
        ShowError "That element cannot be dropped"
    End If

    Exit Sub
NoElement:
    ShowStatus "Element not found"

End Sub

'
'  Start event handler
'
Private Sub IPrimitiveCommandEvents_Start()
    ShowCommand "VBA Drop Command"
    ShowPrompt "Select a droppable element"
    CommandState.SetLocateCursor
End Sub
```
{: .nolineno}


The following subroutine starts the command. 

```vb
Sub DropElements()
    CommandState.StartPrimitive New clsDrop
End Sub
```
{: .nolineno}

### LocateElement and GetLocatedElement
The DataPoint event handler in this example prints information on all of the elements that can be located using the data point that is passed into it. For each element, it displays the type of the outer most element and a point on the element. If the located element is a complex header, it also displays the type of the located component. 

```vb
Implements IPrimitiveCommandEvents

Private Sub IPrimitiveCommandEvents_DataPoint(Point As Point3d, ByVal View As View)
    Dim ele As Element
    Dim eleComponent As Element
    Dim hitPoint As Point3d

    '
    '   Passing True for the final argument tells LocateElement to calculate the
    '   list of hits and to return the first element in the list.  CommandState
    '   retains this list.  Use LocateElement with False for the final argument
    '   to step through the list.
    '
    Set ele = CommandState.LocateElement(Point, View, True)
    Debug.Print "Input Point: " & Point3dToString(Point)

    Do While Not ele Is Nothing
        Debug.Print "  Element type: " & ele.Type
        '
        '   LocateElement always projects the point onto the element.
        '   CommandState.GetHitPoint returns the projected point
        '
        hitPoint = CommandState.GetHitPoint
        Debug.Print "    Point on Element " & Point3dToString(hitPoint)

        '
        '   LocateElement returns the outer most element. If the element is in a cell,
        '   then LocateElement returns the cell header.  A program can use
        '   GetLocatedElement to find the component that LocateElement found.
        '
        If ele.IsComplexElement Then
            Set eleComponent = CommandState.GetLocatedElement(False)
            Debug.Print "    Component type: " & eleComponent.Type
        End If

        '
        '   Get the next element in the list.
        '
        Set ele = CommandState.LocateElement(Point, View, False)
    Loop

End Sub
Private Sub IPrimitiveCommandEvents_Start()
    CommandState.SetLocateCursor
End Sub
Function Point3dToString(pnt As Point3d) As String
    Point3dToString = "(" & pnt.X & ", " & pnt.Y & ", " & pnt.Z & ")"
End Function

'
''  The remaining methods of IPrimitiveCommandEvents were omitted because they are
''  not used in this example.
'
```
{: .nolineno}